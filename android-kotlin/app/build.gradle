apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.example.skeleton"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro',
                    'proguard-fresco.pro'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro',
                    'proguard-fresco.pro'
        }
    }
    // Product Favors: staging & prod
    flavorDimensions "default"
    productFlavors {
        staging {
            applicationId "com.example.skeleton.staging"
            dimension "default"
        }
        prod {
            // For production we use an id for the customer company
            applicationId "com.company.skeleton"
            dimension "default"
        }
    }
    // Enable JAVA8 and lambda
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        abortOnError true
        xmlReport false
        htmlReport true
        lintConfig file("lint.xml")
        htmlOutput file("lint/lint-result.html")
        xmlOutput file("lint/lint-result.xml")
    }
    testOptions {
        unitTests {
            returnDefaultValues = true
        }
    }
}

configurations {
    ktlint
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'

    ktlint "com.github.shyiko:ktlint:0.15.0"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation 'com.android.support:appcompat-v7:27.0.2'
    implementation 'com.android.support:support-core-utils:27.0.2'
    implementation 'com.android.support:design:27.0.2'
    implementation 'com.android.support:cardview-v7:27.0.2'
    implementation 'com.android.support:recyclerview-v7:27.0.2'
    // RxAndroid: https://github.com/ReactiveX/RxAndroid
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    // RxJava: https://github.com/ReactiveX/RxJava
    implementation 'io.reactivex.rxjava2:rxjava:2.1.6'
    // RxKotlin: https://github.com/ReactiveX/RxKotlin
    implementation 'io.reactivex.rxjava2:rxkotlin:2.2.0'
    // Redux: https://github.com/pardom/redux-kotlin
    implementation 'com.github.pardom:redux-kotlin:1.2.1'
    // Conductor: https://github.com/bluelinelabs/Conductor
    implementation 'com.bluelinelabs:conductor:2.1.4'
    // Better ImageView: https://github.com/facebook/fresco
    implementation 'com.facebook.fresco:fresco:1.7.1'
    // Flexbox: https://github.com/google/flexbox-layout
    implementation 'com.google.android:flexbox:0.3.0'
    // OkHttp: https://github.com/square/okhttp
    implementation 'com.squareup.okhttp3:okhttp:3.9.1'
    // MPAndroidChart https://github.com/PhilJay/MPAndroidChart
    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.3'
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "src/main/**/*.kt", "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // to generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // see https://github.com/shyiko/ktlint#usage for more
}
task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "-F", "src/main/**/*.kt"
}
check.dependsOn ktlint
